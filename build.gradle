description "Remove automatic transactions from tapestry-hibernate"

project.ext {
    // For sub-project dependencies
    t5Version = "5.3.7"

    canDeploy = project.hasProperty("hlsDeployUserName") && project.hasProperty("hlsDeployPassword") && project.hasProperty("signing.keyId")

    deployRepositoryRoot = "scp://ssh.phx.nearlyfreespeech.net/home/public/repository"
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: "signing"

    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'

    version = '0.0.1-SNAPSHOT'

    group = 'com.howardlewisship.t5-hibernate-notx'

    repositories {
        mavenCentral()

        // All things JBoss/Javassist/Hibernate
        maven {
            name "JBoss"
            url "http://repository.jboss.org/nexus/content/groups/public/"
        }
    }

    if (canDeploy) {

        configurations {

            configurations.all {
                resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            }

            published.extendsFrom archives, signatures
        }

        signing {
            sign configurations.archives
        }


        configurations {
            deployerJars
        }

        dependencies {
            deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
        }

        project.ext.

        uploadArchives {
            repositories {

                project.ext.deployer = repositories.mavenDeployer {

                    pom.project {
                        inceptionYear "2014"
                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }
                    }

                    configuration = configurations.deployerJars

                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    snapshotRepository(url: "$deployRepositoryRoot/snapshots") {
                        // I'd prefer to authenticate using public key, but Gradle(Maven(Ant)) just prompts
                        // for the password instead, so I have to keep a real password in my
                        // ~/.gradle/settings.gradle.
                        authentication(userName: hlsDeployUserName, password: hlsDeployPassword)
                    }
                }
            }
        }
    }

}